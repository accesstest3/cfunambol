; Script generated by the HM NIS Edit Script Wizard.
; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Funambol"
!define PRODUCT_PUBLISHER "Funambol"
!define PRODUCT_WEB_SITE "http://www.funambol.com"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${PK_NAME}.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME} ${PRODUCT_VERSION}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${DIR_OUTPUT}/ds-server/icons/install.ico"
!define MUI_UNICON "${DIR_OUTPUT}/ds-server/icons/uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME

; License page
!define MUI_LICENSEPAGE_CHECKBOX
!insertmacro MUI_PAGE_LICENSE "presetup\License.txt"

; Directory page, the first two define serve to check if the install di is correct
!define MUI_DIRECTORYPAGE_VERIFYONLEAVE
!define MUI_PAGE_CUSTOMFUNCTION_LEAVE CheckInstallDir
!insertmacro MUI_PAGE_DIRECTORY

; Start menu page
var ICONS_GROUP
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "Funambol"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page

!define MUI_FINISHPAGE_RUN "$INSTDIR\bin\restartall.cmd"
!define MUI_FINISHPAGE_RUN_TEXT "Start Funambol DS Server"
!define MUI_FINISHPAGE_SHOWREADME "http://funambol.com/docs/v90/funambol-installation-and-administration-guide.pdf"
!define MUI_FINISHPAGE_SHOWREADME_TEXT "Open Funambol Administration Guide"
!define MUI_FINISHPAGE_RUN_CHECKED

!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "..\${PK_NAME}.exe"
InstallDir "$PROGRAMFILES\${INSTALL_DIR}"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show
Icon "${MUI_ICON}"

RequestExecutionLevel admin

Section "MainSection" SEC01

  ;check if in the instdir path there is some slash
  Push $INSTDIR
  Push "/"
  Call StrSlash
  Pop  $R0
  StrCpy $INSTDIR $R0
    
  ;check if in the start menu path there is some slash
  Push $ICONS_GROUP
  Push "/"
  Call StrSlash
  Pop  $R0
  StrCpy $ICONS_GROUP $R0

  SetOutPath "$INSTDIR"
  SetOverwrite on
  File /r "${DIR_OUTPUT}\*.*"

; Shortcuts
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  SetShellVarContext all

  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP\Data Synchronization Server"
  
  SetOutPath "$INSTDIR\admin\bin"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Administration Tool.lnk" "$INSTDIR\admin\bin\funamboladmin.exe" "" "$INSTDIR\admin\icons\icon_admin.ico" "" "" "" "Launch Administration Tool"

  SetOutPath "$INSTDIR"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Data Synchronization Server\Start Server.lnk" "$INSTDIR\bin\restartall.cmd" "" "$INSTDIR\ds-server\icons\icon_go.ico" "" "" "" "Launch Data Synchronization Server"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Data Synchronization Server\Stop Server.lnk" "$INSTDIR\bin\stopall.cmd" "" "$INSTDIR\ds-server\icons\icon_stop.ico" "" "" "" "Stop Data Synchronization Server"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Data Synchronization Server\View Log.lnk" "$INSTDIR\logs\ds-server\ds-server.log" "" "" "" "" "" "Data Synchronization Server log"
  
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Documentation.lnk" "http://www.forge.funambol.org/download/documentation.html" "" "" "" "" "" "Documentation"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Web Demo Client.lnk" "http://localhost:8080/funambol" "" "" "" "" "" "Launch Web Demo Client"
  
  SetOutPath "$INSTDIR\plug-ins\javademo"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Java Demo Client.lnk" "$INSTDIR\plug-ins\javademo\bin\demo.cmd" "" "$INSTDIR\plug-ins\javademo\images\icon_menu.ico" "" SW_SHOWMINIMIZED "" "Launch Java Demo Client"
  
  SetOutPath "$INSTDIR"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Readme.lnk" "$INSTDIR\Readme.txt" "" "" "" "" "" "Readme"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd


Section -AdditionalIcons
  SetOutPath $INSTDIR
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  SetShellVarContext all
  
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\www.funambol.com.lnk" "http://www.funambol.com" "" "" "" "" "" "Funambol Web Site"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe" "" "" "" "" "" "Uninstall Funambol"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd


Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\bin\restartall.cmd"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\ds-server\icons\icon_logo.ico"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

; Check if current user have Administrator rights to run the installer.
; If not, the installer will be aborted.
Function CheckUserRights

 UserInfo::GetAccountType
 Pop $R0
 StrCmp $R0 "Admin" done1
 MessageBox MB_OK "You need Administrator rights to install ${PRODUCT_NAME}."
 Abort
 done1:
  Return

FunctionEnd


; Check if current user have Administrator rights to run the uninstaller.
; If not, the uninstaller will be aborted.
Function un.CheckUserRights

 UserInfo::GetAccountType
 Pop $R0
 StrCmp $R0 "Admin" done1
 MessageBox MB_OK "You need Administrator rights to uninstall ${PRODUCT_NAME}."
 Abort
 done1:
  Return

FunctionEnd

Function un.CheckAdministrationTool
  loop1:
    FindWindow $0 "org.netbeans.core.windows.view.ui.MainWindow" "Funambol Administration Tool"
    IntCmp $0 0 done1
    MessageBox MB_OKCANCEL "Please, close Administration Tool to continue." IDCANCEL Cancel
    goto loop1
  done1:
    Return

  Cancel:
    MessageBox MB_OK "Uninstallation failed."
    Abort
FunctionEnd

Function un.CheckJavaGUI
  loop1:
    FindWindow $0 "com.funambol.syncclient.javagui.MainWindow" "Funambol Java Demo Client"
    IntCmp $0 0 done1
    MessageBox MB_OKCANCEL "Please, close Funambol Java Demo Client to continue." IDCANCEL Cancel
    goto loop1
  done1:
    Return

  Cancel:
    MessageBox MB_OK "Uninstallation failed."
    Abort
FunctionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd


Function un.onInit

  Call un.CheckUserRights

  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove \
      $(^Name) and all of its components?$\n$\nWARNING! The uninstaller may also delete Funambol Settings \
      and data directories.$\nPlease backup the data which is stored in subdirectories of Funambol." IDYES +2
  Abort

  Call un.CheckAdministrationTool
  Call un.CheckJavaGUI
FunctionEnd
 
 
Function .onInit

  Call CheckUserRights
  Call GetParameters
  
  ReadRegStr $R0 HKLM \
      "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME} ${PRODUCT_VERSION}" \
      "UninstallString"
  StrCmp $R0 "" done
     
  MessageBox MB_OK|MB_ICONEXCLAMATION \
      "${PRODUCT_NAME} ${PRODUCT_VERSION} is already installed. $\n$\nPlease uninstall the \
      previous version." 
  Abort   
      
  done:
  ; remove the copied uninstaller
  Delete '$0'
FunctionEnd


;Check if the install directory is empty
Function CheckInstallDir
  ;if the directory is empty a message box is displayed otherwise there is a jump of 3 line in the script
  IfFileExists $INSTDIR\*.* 0 +3
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 \
     "The selected folder already exists.$\n$\nWARNING: Please uninstall any existing version of Funambol before proceeding with this installation!$\n$\nDo you want to proceed with the installation?" IDYES +2
  Abort 
  
  IfFileExists $INSTDIR\bin\fnblstatus.exe 0
  ExecWait "$INSTDIR\bin\fnblstatus.exe /stop"

  IfFileExists $INSTDIR\bin\stopall.cmd 0
  SetOutPath "$INSTDIR"
  ExecWait "bin\stopall.cmd"

FunctionEnd


; GetParameters
; input, none
; output, top of stack (replaces, with e.g. whatever)
; modifies no other variables.
Function GetParameters
   Push $R0
   Push $R1
   Push $R2
   Push $R3

   StrCpy $R2 1
   StrLen $R3 $CMDLINE

   ;Check for quote or space
   StrCpy $R0 $CMDLINE $R2
   StrCmp $R0 '"' 0 +3
     StrCpy $R1 '"'
     Goto loop
   StrCpy $R1 " "

   loop:
     IntOp $R2 $R2 + 1
     StrCpy $R0 $CMDLINE 1 $R2
     StrCmp $R0 $R1 get
     StrCmp $R2 $R3 get
     Goto loop

   get:
     IntOp $R2 $R2 + 1
     StrCpy $R0 $CMDLINE 1 $R2
     StrCmp $R0 " " get
    StrCpy $R0 $CMDLINE "" $R2

   Pop $R3
   Pop $R2
   Pop $R1
   Exch $R0

 FunctionEnd

Section Uninstall
  ExecWait "$INSTDIR\bin\stopall.cmd"

  ExecWait "$INSTDIR\bin\fnblstatus.exe /stop"

  !insertmacro MUI_STARTMENU_GETFOLDER "Application" $ICONS_GROUP
  SetShellVarContext all
  
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete /REBOOTOK "$INSTDIR\*.*"

  Delete "$SMPROGRAMS\$ICONS_GROUP\Data Synchronization Server\*.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Data Synchronization Server"

  Delete "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Funambol Web Site.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\*.lnk"  

  RMDir "$SMPROGRAMS\$ICONS_GROUP\Data Synchronization Server"
  RMDir "$SMPROGRAMS\$ICONS_GROUP\Funambol Plug-Ins"
  RMDir "$SMPROGRAMS\$ICONS_GROUP"
  StrCpy $R4 "$ICONS_GROUP"    
  loop:
    Push "$R4"
    Call un.GetLastStrPart
    Pop $R0   
    StrCmp $R4 $R0 +4
    StrCpy $R4 $R0
    RMDir "$SMPROGRAMS\$R0"
    Goto loop

  ; set path to the root in order to remove also the 'INSTDIR'
  SetOutPath "\"
  
  RMDir /r /REBOOTOK "$INSTDIR\admin"
  RMDir /r /REBOOTOK "$INSTDIR\bin"
  RMDir /r /REBOOTOK "$INSTDIR\ds-server"
  RMDir /r /REBOOTOK "$INSTDIR\inbox-listener"  
  RMDir /r /REBOOTOK "$INSTDIR\pim-listener"
  RMDir /r /REBOOTOK "$INSTDIR\ctp-server"
  RMDir /r /REBOOTOK "$INSTDIR\tools"
  RMDir /r /REBOOTOK "$INSTDIR\docs"
  RMDir /r /REBOOTOK "$INSTDIR\plug-ins\javademo"
  RMDir /r /REBOOTOK "$INSTDIR\plug-ins\cl"
  RMDir /r /REBOOTOK "$INSTDIR\logs"
  RMDir /r /REBOOTOK "$INSTDIR\LICENSES"
  RMDir /r /REBOOTOK "$INSTDIR\config"
  Delete "$INSTDIR\*.*"
    
  RMDir  /REBOOTOK "$INSTDIR\plug-ins"
  RMDir  /REBOOTOK "$INSTDIR"
  StrCpy $R4 "$INSTDIR"
  loop1:
    Push "$R4"
    Call un.GetLastStrPart
    Pop $R0   
    StrCmp $R4 $R0 +5
    StrCmp "$PROGRAMFILES" $R0 +4
    StrCpy $R4 $R0
    RMDir "$R0"
    Goto loop1

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  
  SetAutoClose true
SectionEnd


; get the last part of a string using '\' as the separator
Function un.GetLastStrPart
  Exch $R0
  Push $R1
  Push $R2
  StrLen $R1 $R0
  IntOp $R1 $R1 + 1
  StrCpy $R3 1
  loop:
    IntOp $R1 $R1 - 1
    StrCpy $R2 $R0 1 -$R3
    IntOp $R3 $R3 + 1
    StrCmp $R2 "" exit2
    StrCmp $R2 "\" exit1
  Goto loop
  exit1:
    IntOp $R3 $R3 - 1
    StrCpy $R0 $R0 -$R3
  exit2:
    Pop $R2
    Pop $R1
    Exch $R0
FunctionEnd


;transform a string: where there is  a '/' it becomes '\'
Function StrSlash
  Exch $R3 ; $R3 = needle ("\" or "/")
  Exch
  Exch $R1 ; $R1 = String to replacement in (haystack)
  Push $R2 ; Replaced haystack
  Push $R4 ; $R4 = not $R3 ("/" or "\")
  Push $R6
  Push $R7 ; Scratch reg
  StrCpy $R2 ""
  StrLen $R6 $R1
  StrCpy $R4 "\"
  StrCmp $R3 "/" loop
  StrCpy $R4 "/"  
loop:
  StrCpy $R7 $R1 1
  StrCpy $R1 $R1 $R6 1
  StrCmp $R7 $R3 found
  StrCpy $R2 "$R2$R7"
  StrCmp $R1 "" done loop
found:
  StrCpy $R2 "$R2$R4"
  StrCmp $R1 "" done loop
done:
  StrCpy $R3 $R2
  Pop $R7
  Pop $R6
  Pop $R4
  Pop $R2
  Pop $R1
  Exch $R3
FunctionEnd
