; Script generated by the HM NIS Edit Script Wizard.
; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Funambol Java Demo Client"
!define PRODUCT_PUBLISHER "Funambol"
!define PRODUCT_WEB_SITE "http://www.funambol.com"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${PK_NAME}.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME} ${PRODUCT_VERSION}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_STARTMENU_REGVAL "NSIS:StartMenuDir"
!define JRE_URL " http://javadl.sun.com/webapps/download/AutoDL?BundleId=11145"
!define JRE_VERSIONS_ALLOWED "1.5 or 1.6"
!define JRE_VERSION_DOWNLOAD "1.6"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "icons\install.ico"
!define MUI_UNICON "icons\uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME

; License page
!define MUI_LICENSEPAGE_CHECKBOX
!insertmacro MUI_PAGE_LICENSE "presetup\LICENSE.txt"

; Directory page, the first two define serve to check if the install di is correct
!define MUI_DIRECTORYPAGE_VERIFYONLEAVE
!define MUI_PAGE_CUSTOMFUNCTION_LEAVE CheckInstallDir
!insertmacro MUI_PAGE_DIRECTORY

; Start menu page
var ICONS_GROUP
!define MUI_STARTMENUPAGE_NODISABLE
!define MUI_STARTMENUPAGE_DEFAULTFOLDER "Funambol\Java Demo Client"
!define MUI_STARTMENUPAGE_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "${PRODUCT_STARTMENU_REGVAL}"
!insertmacro MUI_PAGE_STARTMENU Application $ICONS_GROUP

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES

; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\bin\demo.cmd"
!define MUI_FINISHPAGE_SHOWREADME "$INSTDIR\Readme.txt"
!define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------


Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "${OUTFILE}"
InstallDir "$PROGRAMFILES\${INSTALL_DIR}"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show
Icon "${MUI_ICON}"

RequestExecutionLevel admin

Section "MainSection" SEC01

  ;check if in the instdir path there is some slash
  Push $INSTDIR
  Push "/"
  Call StrSlash
  Pop  $R0
  StrCpy $INSTDIR $R0
    
  ;check if in the start menu path there is some slash
  Push $ICONS_GROUP
  Push "/"
  Call StrSlash
  Pop  $R0
  StrCpy $ICONS_GROUP $R0

  SetOutPath "$INSTDIR"
  SetOverwrite on
  File /r "${DIR_OUTPUT}\*.*"
  File "launchw.exe"
  SetOutPath "$INSTDIR\images"
  File "icons\*.*"

; Shortcuts
  SetOutPath $INSTDIR
  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
  SetShellVarContext all
  
  CreateDirectory "$SMPROGRAMS\$ICONS_GROUP"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Funambol Java Demo Client.lnk" "$INSTDIR\bin\demo.cmd" "" "$INSTDIR\images\icon_menu.ico" "" SW_SHOWMINIMIZED "" "Launch Funambol Java Demo Client ${PRODUCT_VERSION}"
  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Readme.lnk" "$INSTDIR\Readme.txt" "" "" "" "" "" "Funambol Java Demo Client Readme"

WriteINIStr "$INSTDIR\funambol.url" "InternetShortcut" "URL" "http://www.funambol.com"
CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Funambol Web Site.lnk" "$INSTDIR\funambol.url" "" "$INSTDIR\funambol.url" 0

  CreateShortCut "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk" "$INSTDIR\uninst.exe" "" "" "" "" "" "Uninstall Funambol Java Demo Client ${PRODUCT_VERSION}"
  !insertmacro MUI_STARTMENU_WRITE_END
SectionEnd


Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\demo.cmd"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\images\icon_menu.ico"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

Function CheckJRE16
  # read the value from the registry into the $0 register
  readRegStr $0 HKLM "SOFTWARE\JavaSoft\Java Runtime Environment\1.6" "JavaHome"
  IfErrors noJRE16 JREfound
  noJRE16:
    call getJRE16
  JREfound:
FunctionEnd

;checks if jre 1.5 is installed
Function CheckJRE
  # read the value from the registry into the $0 register
  readRegStr $0 HKLM "SOFTWARE\JavaSoft\Java Runtime Environment\1.5" "JavaHome"
  IfErrors noJRE15 JREfound
  noJRE15:
    call CheckJRE16
  JREfound:
FunctionEnd

Function GetJRE16
  MessageBox MB_OK "${PRODUCT_NAME} uses Java ${JRE_VERSIONS_ALLOWED} , Java ${JRE_VERSION_DOWNLOAD} will now be downloaded and installed"
  StrCpy $2 "$TEMP\jre.exe"
  inetc::get /BANNER "Downloading Java ${JRE_VERSION_DOWNLOAD} ..." "${JRE_URL}" "$2"

  Pop $0 ;Get the return value
  StrCmp $0 "OK" +3
  MessageBox MB_OK "Download failed! Please go to www.java.sun.com."
  Quit
  
  ExecWait $2
  Delete $2
FunctionEnd



Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd


Function un.onInit

  Call un.CheckUserRights

  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
  
  Call un.CheckClientDemo
FunctionEnd


Function .onInit

  Call CheckUserRights
  Call GetParameters

  Call CheckJRE
  
  ;sometimes the $INSTDIR is not correctly passed to NSIS so is
  ;necessary to redefine it with this command
  StrCpy $INSTDIR "$PROGRAMFILES\${INSTALL_DIR}"
  
  ReadRegStr $R0 HKLM \
    "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME} ${PRODUCT_VERSION}" \
    "UninstallString"
  StrCmp $R0 "" done
   
  MessageBox MB_OK|MB_ICONEXCLAMATION \
    "${PRODUCT_NAME} ${PRODUCT_VERSION} is already installed. $\n$\nPlease uninstall the \
    previous version." 
  Abort   
    
  done:
  ; remove the copied uninstaller
    Delete '$0'    
FunctionEnd


;Check if the install directory is empty
Function CheckInstallDir
  ;if the directory is empty a message box is displayed otherwise there is a jump of 3 line in the script
  IfFileExists $INSTDIR\*.* 0 +3
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 \
    "You've specified a folder which already exists. Perhaps some application was previously installed there.$\n$\nAre you sure you want to install ${PRODUCT_NAME} to this folder?" IDYES +2
  Abort 
    
FunctionEnd


; Check if the application is still running when the user wants to uninstall it
Function un.CheckClientDemo
  loop1:
    FindWindow $0 "com.funambol.syncclient.javagui.MainWindow" "${PRODUCT_NAME}"
    IntCmp $0 0 done1
    MessageBox MB_OKCANCEL "Please, close ${PRODUCT_NAME} to continue." IDCANCEL Cancel
    goto loop1
  done1:
    Return

  Cancel:
    MessageBox MB_OK "Uninstallation failed."
    Abort
FunctionEnd


; Check if current user have Administrator rights to run the installer.
; If not, the installer will be aborted.
Function CheckUserRights

 UserInfo::GetAccountType
 Pop $R0
 StrCmp $R0 "Admin" done1
 MessageBox MB_OK "You need Administrator rights to install ${PRODUCT_NAME}."
 Abort
 done1:
  Return

FunctionEnd


; Check if current user have Administrator rights to run the uninstaller.
; If not, the uninstaller will be aborted.
Function un.CheckUserRights

 UserInfo::GetAccountType
 Pop $R0
 StrCmp $R0 "Admin" done1
 MessageBox MB_OK "You need Administrator rights to uninstall ${PRODUCT_NAME}."
 Abort
 done1:
  Return

FunctionEnd


 ; GetParameters
 ; input, none
 ; output, top of stack (replaces, with e.g. whatever)
 ; modifies no other variables.
 Function GetParameters
   Push $R0
   Push $R1
   Push $R2
   Push $R3
   
   StrCpy $R2 1
   StrLen $R3 $CMDLINE

   ;Check for quote or space
   StrCpy $R0 $CMDLINE $R2
   StrCmp $R0 '"' 0 +3
     StrCpy $R1 '"'
     Goto loop
   StrCpy $R1 " "

   loop:
     IntOp $R2 $R2 + 1
     StrCpy $R0 $CMDLINE 1 $R2
     StrCmp $R0 $R1 get
     StrCmp $R2 $R3 get
     Goto loop

   get:
     IntOp $R2 $R2 + 1
     StrCpy $R0 $CMDLINE 1 $R2
     StrCmp $R0 " " get
     StrCpy $R0 $CMDLINE "" $R2

   Pop $R3
   Pop $R2
   Pop $R1
   Exch $R0
 
 FunctionEnd


Section Uninstall
  !insertmacro MUI_STARTMENU_GETFOLDER "Application" $ICONS_GROUP
  SetShellVarContext all
  
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\*.*"
  
  Delete "$SMPROGRAMS\$ICONS_GROUP\Uninstall.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Readme.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Funambol Web Site.lnk"
  Delete "$SMPROGRAMS\$ICONS_GROUP\Funambol Java Demo Client.lnk"
  
  ; set path to the root in order to remove also the 'INSTDIR'
  SetOutPath "\"
  
  RMDir /r "$INSTDIR"
  StrCpy $R4 "$INSTDIR"
  loop1:
    Push "$R4"
    Call un.GetLastStrPart
    Pop $R0   
    StrCmp $R4 $R0 +5
    StrCmp "$PROGRAMFILES" $R0 +4
    StrCpy $R4 $R0
    RMDir "$R0"
    Goto loop1      
  
  RMDir "$SMPROGRAMS\$ICONS_GROUP"  
  StrCpy $R4 "$ICONS_GROUP"    
  loop:
    Push "$R4"
    Call un.GetLastStrPart
    Pop $R0   
    StrCmp $R4 $R0 +4
    StrCpy $R4 $R0
    RMDir "$SMPROGRAMS\$R0"
    Goto loop
  
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true 
SectionEnd


; get the last part of a string using '\' as the separator
Function un.GetLastStrPart
  Exch $R0
  Push $R1
  Push $R2
  StrLen $R1 $R0
  IntOp $R1 $R1 + 1
  StrCpy $R3 1
  loop:
    IntOp $R1 $R1 - 1
    StrCpy $R2 $R0 1 -$R3
    IntOp $R3 $R3 + 1
    StrCmp $R2 "" exit2
    StrCmp $R2 "\" exit1
  Goto loop
  exit1:
    IntOp $R3 $R3 - 1
    StrCpy $R0 $R0 -$R3
  exit2:
    Pop $R2
    Pop $R1
    Exch $R0
FunctionEnd


;transform a string: where there is  a '/' it becomes '\'
Function StrSlash
  Exch $R3 ; $R3 = needle ("\" or "/")
  Exch
  Exch $R1 ; $R1 = String to replacement in (haystack)
  Push $R2 ; Replaced haystack
  Push $R4 ; $R4 = not $R3 ("/" or "\")
  Push $R6
  Push $R7 ; Scratch reg
  StrCpy $R2 ""
  StrLen $R6 $R1
  StrCpy $R4 "\"
  StrCmp $R3 "/" loop
  StrCpy $R4 "/"  
loop:
  StrCpy $R7 $R1 1
  StrCpy $R1 $R1 $R6 1
  StrCmp $R7 $R3 found
  StrCpy $R2 "$R2$R7"
  StrCmp $R1 "" done loop
found:
  StrCpy $R2 "$R2$R4"
  StrCmp $R1 "" done loop
done:
  StrCpy $R3 $R2
  Pop $R7
  Pop $R6
  Pop $R4
  Pop $R2
  Pop $R1
  Exch $R3
FunctionEnd
